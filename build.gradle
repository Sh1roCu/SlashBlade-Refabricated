plugins {
    id 'fabric-loom' version '1.11-SNAPSHOT'
    id 'maven-publish'
}

version = project.mod_version
group = project.maven_group

base {
    archivesName = project.archives_base_name
}


fabricApi {
    configureDataGeneration {
        client = true
    }
}

loom {
    accessWidenerPath = file("src/main/resources/slashblade.accesswidener")
    mixin {
        defaultRefmapName = "slashblade.refmap.json"
    }
}
repositories {
    flatDir {
        dir "libs"
    }
    mavenLocal()
    mavenCentral()
    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }
    maven {
        name = "Ladysnake Mods"
        url = 'https://maven.ladysnake.org/releases'
    }
    maven {
        name = "Fuzs Mod Resources"
        url = "https://raw.githubusercontent.com/Fuzss/modresources/main/maven/"
    }
    maven {
        name "KosmX's maven"
        url 'https://maven.kosmx.dev/'
    }

    maven {
        name = "ModMaven"
        url = "https://modmaven.dev"
    }

    maven {
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/"
    }

    maven {
        url "https://cursemaven.com"
    }
    maven {
        url "https://maven.jamieswhiteshirt.com/libs-release"
        content {
            includeGroup "com.jamieswhiteshirt"
        }
    }
    maven { url = "https://mvn.devos.one/releases/" }
}

dependencies {
    minecraft "com.mojang:minecraft:${project.minecraft_version}"
    //mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
    mappings loom.layered() {
        officialMojangMappings()
        //parchment("org.parchmentmc.data:parchment-${project.parchment_minecraft_version}:${project.parchment_mappings_version}@zip")
    }
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modApi(include("io.github.fabricators_of_create.Porting-Lib:obj_loader:2.3.8+1.20.1"))

    modCompileOnly 'com.google.code.findbugs:jsr305:3.0.2'
    annotationProcessor 'com.google.code.findbugs:jsr305:3.0.2'

    // 触及距离/攻击范围
    modImplementation(include("com.jamieswhiteshirt:reach-entity-attributes:2.4.0"))

    // CCA
    modImplementation(include("dev.onyxstudios.cardinal-components-api:cardinal-components-base:${project.cca_version}"))
    modImplementation(include("dev.onyxstudios.cardinal-components-api:cardinal-components-item:${project.cca_version}"))
    modImplementation(include("dev.onyxstudios.cardinal-components-api:cardinal-components-entity:${project.cca_version}"))

    modApi "fuzs.forgeconfigapiport:forgeconfigapiport-fabric:8.0.2"

    modImplementation "dev.kosmx.player-anim:player-animation-lib-fabric:1.0.2-rc1+1.20"
    //假人(含前置)
    modRuntimeOnly "curse.maven:selene-499980:6815736"
    modRuntimeOnly "curse.maven:mmmmmmmmmmmm-225738:6608232"
    // compile against the JEI API but do not include it at runtime
    modCompileOnly "mezz.jei:jei-${minecraft_version}-common-api:${jei_version}"
    modCompileOnly "mezz.jei:jei-${minecraft_version}-fabric-api:${jei_version}"
    // at runtime, use the full JEI jar for Fabric
    modRuntimeOnly "mezz.jei:jei-${minecraft_version}-fabric:${jei_version}"
    //Configured
    modRuntimeOnly "curse.maven:configured-457570:5180902"

    modRuntimeOnly "curse.maven:nbtedit-reborn-678133:5984629"

    //神化
    //modImplementation "curse.maven:zenith-attributes-910078:6031736"
    //modImplementation "curse.maven:fakerlib-853197:5503724"
    //modImplementation "curse.maven:zenith-620928:6638815"

    //史诗战斗
    //modRuntimeOnly "curse.maven:epic-fight-mod-"

    //暮色森林
    //modCompileOnly "curse.maven:the-twilight-forest-unofficial-1111023:6010673"
    modCompileOnly "libs:twilightforest-fabric-1.20.1:1.4.7"
    modApi('teamreborn:energy:3.0.0') {
        exclude(group: "net.fabricmc.fabric-api")
    }

    //modCompileOnly "dev.emi:emi-fabric:${emi_version}:api")
    //modRuntimeOnly "dev.emi:emi-fabric:${emi_version}")

    //FTB Quests
    modImplementation "curse.maven:architectury-api-419699:5137936"
    modImplementation "curse.maven:ftb-quests-fabric-438496:6431736"
    modImplementation "curse.maven:ftb-library-fabric-438495:6807422"
    modImplementation "curse.maven:ftb-teams-fabric-438497:6130783"
    modImplementation "curse.maven:item-filters-309674:4838265"
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft_version", project.minecraft_version
    inputs.property "loader_version", project.loader_version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version,
                "minecraft_version": project.minecraft_version,
                "loader_version": project.loader_version
    }
}

def targetJavaVersion = 17
tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        it.options.release.set(targetJavaVersion)
    }
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
    withSourcesJar()
}

jar {
    from("LICENSE") {
        rename { "${it}_${project.archivesBaseName}" }
    }
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    repositories {
    }
}
